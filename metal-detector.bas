NEW 
1 DETECTOR 
2 ' Pin point metal detector, version 1, revesion 2  
4  GOSUB CLK.SWITCH 
10 PMODE 6,POUT ' GREEN LED OUTPUT
20 CONST TIM1.ARRH=$5262,TIM1.ARRL=$5263, SENSIVITY=4
30 CONST TIM1.CR1=$5250
40 CONST PB.CR1=$5008: BRES PB.CR1,8
50 CONST ADC.TDRL=$5407: BSET ADC.TDRL,8 
60 CONST LEN=32
70 PWM.EN 1,8 
80 PWM.CH.EN 4,1 
88 ' set PWM frequency to ~53Khz
89 LET F=241
90 POKE TIM1.ARRH,F/256: POKE TIM1.ARRL,F%256:PWM.OUT 4,F/2
100 ADCON 1,4
110 GOSUB LOW.POWER
120 PAUSE 100 
130 FOR I=1 TO LEN : LET S=S+ADCREAD(3):PAUSE 1:NEXT I 
140 LET A=S/LEN  
150 GOSUB CLS : ? "Detector active"
160 DO 
164 GOSUB CLR.C3 
170 LET R=ADCREAD(3) 
180 LET S=S-A+R, A=S/LEN    
190 IF ABS(A-R)>SENSIVITY : GOSUB ALARM  
194 GET K 
200 UNTIL K 
209 ' turn off detector 
210 ADCON 0 : PWM.EN 0 
220 END
249 ' clear screen subroutine
250 CLS 
260 ' clear terminal screen 
270 ' send cursor top-left 
280 ? CHAR(27);"[";CHAR(50);CHAR($4A)
290 ? CHAR(27);"[";CHAR($48)
300 RETURN 
399 ' BEEP and LED on for 10msec 
400 ALARM 
410 DWRITE 6,1:TONE 500,10:DWRITE 6,0
420 RETURN 
499 ' disable unsused peripherals clock
500  LOW.POWER 
510  CONST CLK.PCKENR1  = $50C7, CLK.PCKENR2  = $50CA
520  BRES CLK.PCKENR1,7+64: BRES CLK.PCKENR2,NOT 8 
530  RETURN

599 ' switch to HSE clock 12Mhz crystal 
600 CLK.SWITCH
604 CONST CLK.SWR=$50C4,CLK.SWCR=$50C5
608 CONST UART3.BRR1=$5242,UART3.BBR2=$5243
612 CONST UART3.CR1=$5244, PA.CR1=$5003
616 BRES PA.CR1,6 ' disable pull up on PA:1,2
620 POKE CLK.SWR,$B4
624 DO UNTIL BTEST(CLK.SWCR,3) : ' SWIF bit 
628 BSET CLK.SWCR,2 ' switch to HSE
630 ' adjust UART3 BRR 
632 BRES UART3.CR1,1 : ' turn off UART 
636 POKE UART3.BBR2,8:POKE UART3.BRR1,6 : '0x68 
640 BSET UART3.CR1,1 : ' turn on UART
644 RETURN 

699 ' empty C3
700 CLR.C3  
702 ADCON 0 
704 BSET PORTB+DDR,8
706 BRES PORTB+ODR,8 
708 PAUSE 1 
710 BRES PORTB+DDR,8 
712 ADCON 1,4 
714 PAUSE 2
720 RETURN 
